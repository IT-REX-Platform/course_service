plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id "io.github.kobylynskyi.graphql.codegen" version "5.7.2"
    id "org.sonarqube" version "4.0.0.2929"
    id "jacoco"
    id 'com.adarshr.test-logger' version '3.2.0'
}

group = 'de.unistuttgart.iste.gits'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

def jacocoEnabled = System.properties.getProperty("jacocoEnabled") ?: "true"

// Apply JaCoCo settings only if jacaco is enable
if (jacocoEnabled.toBoolean()) {
    project.logger.lifecycle('Applying jacoco settings from jacoco.gradle')
    apply from: rootProject.file("jacoco.gradle")
}

sonarqube {
    properties {
        property("sonar.projectKey", "IT-REX-Platform_course_service")
        property("sonar.organization", "it-rex-platform")
        property("sonar.host.url", "https://sonarcloud.io")
    }

}

// Automatically generate DTOs from GraphQL schema:
graphqlCodegen {
    // all config options:
    // https://github.com/kobylynskyi/graphql-java-codegen/blob/main/docs/codegen-options.md
    outputDir = new File("$buildDir/generated")
    packageName = "de.unistuttgart.iste.gits.generated.dto"
    generatedAnnotation = "jakarta.annotation.Generated"
    modelValidationAnnotation = "jakarta.validation.constraints.NotNull"
    generateApis = false // set to false as the generator does not support spring boot graphQL
    customTypesMapping = [
            "DateTime" : "java.time.OffsetDateTime",
            "Date"     : "java.time.LocalDate",
            "Time"     : "java.time.OffsetTime",
            "LocalTime": "java.time.LocalTime",
            "UUID"     : "java.util.UUID",
            "Url"      : "java.net.URL",
    ]
    graphqlSchemas {
        rootDir = new File("$projectDir/src/main/resources/graphql/service")
    }
    generateEqualsAndHashCode = true
    generateToString = true
}

// Automatically generate GraphQL code on project build:
compileJava.dependsOn 'graphqlCodegen'

// Add generated sources to your project source sets:
sourceSets.main.java.srcDir "$buildDir/generated"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('de.unistuttgart.iste.gits:gits-common') {
        version {
            branch = 'main'
        }
    }
    implementation 'com.google.code.findbugs:jsr305:3.0.2' // removes a gradle warning about an unknown annotation
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-graphql'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.modelmapper:modelmapper:3.+'
    implementation 'com.graphql-java:graphql-java-extended-scalars:20.0'
    implementation 'com.graphql-java:graphql-java-extended-validation:20.0'
    implementation 'io.dapr:dapr-sdk:1.9.0' // Dapr's core SDK with all features, except Actors.
    implementation 'io.dapr:dapr-sdk-springboot:1.9.0' // Dapr's SDK integration with SpringBoot
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation('de.unistuttgart.iste.gits:gits-common-test') {
        version {
            branch = 'main'
        }
    }
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.springframework.graphql:spring-graphql-test'
    testImplementation "org.mockito:mockito-core:3.+"
    testImplementation 'org.hamcrest:hamcrest:2.+'
    testImplementation "org.testcontainers:postgresql:1.18.3"
    testImplementation "org.testcontainers:junit-jupiter:1.18.3"
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach { testLogging.showStandardStreams = true }
